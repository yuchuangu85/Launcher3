import com.android.build.gradle.api.AndroidBasePlugin
import org.jetbrains.kotlin.gradle.dsl.JvmDefaultMode
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.android.test) apply false
    alias(libs.plugins.androidx.baselineprofile)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.google.ksp)
    alias(libs.plugins.google.protobuf)
}

allprojects {
    plugins.withType(AndroidBasePlugin).configureEach {
        android {
            buildToolsVersion "36.1.0"
            compileSdk {
                version = release(36) {
                    minorApiLevel = 1
                }
            }
            defaultConfig {
                minSdk = 26
                targetSdk = 36
                vectorDrawables.useSupportLibrary = true
            }
            lint {
                abortOnError true
                checkReleaseBuilds false
            }
            compileOptions {
                sourceCompatibility = libs.versions.jdkRelease.get()
                targetCompatibility = libs.versions.jdkRelease.get()
            }
        }
        dependencies {
            implementation libs.androidx.core.ktx
            implementation libs.androidx.core.animation
        }
    }

    plugins.withId('com.google.protobuf') {
        protobuf {
            // Configure the protoc executable
            protoc {
                artifact = libs.protobuf.protoc.get().toString()
            }
            generateProtoTasks {
                all().configureEach { task ->
                    task.builtins {
                        remove java
                        java {
                            option "lite"
                        }
                    }
                }
            }
        }
        dependencies {
            implementation libs.protobuf.javalite
        }
    }

    plugins.withType(JavaBasePlugin).configureEach {
        java {
            sourceCompatibility = libs.versions.jdkRelease.get()
            targetCompatibility = libs.versions.jdkRelease.get()
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        compilerOptions {
            jvmDefault = JvmDefaultMode.ENABLE
            jvmTarget = JvmTarget.fromTarget(libs.versions.jdkRelease.get())
        }
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency {
            // Fix duplicate class kotlinx.android.parcel.* found in these 2 dependencies.
            if (requested.module == libs.kotlin.androidExtensionRuntime.get().module) {
                useTarget(libs.kotlin.parcelizeRuntime)
            }
        }
    }

    ext {
        FRAMEWORK_PREBUILTS_DIR = "$rootDir/prebuilts/libs"

        addFrameworkJar = { String name ->
            def frameworkJar = new File(FRAMEWORK_PREBUILTS_DIR, name)
            if (!frameworkJar.exists()) {
                throw new IllegalArgumentException("Framework jar path ${frameworkJar.path} doesn't exist")
            }
            gradle.projectsEvaluated {
                tasks.withType(JavaCompile).configureEach {
                    // Make sure the frameworkJar is always first in the classpath.
                    classpath = files(frameworkJar, classpath)
                }
                tasks.withType(KotlinCompile).configureEach {
                    // Make sure the frameworkJar is always first in the classpath.
                    libraries.setFrom files(frameworkJar, libraries)
                }
            }
            dependencies {
                compileOnly files(frameworkJar)
            }
        }

        compileOnlyCommonJars = {
            dependencies {
                compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-core-16.jar')
                compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-statsd-16.jar')
                compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'WindowManager-Shell-16.jar')

                compileOnly projects.compatLib
                compileOnly projects.compatLib.compatLibVQ
                compileOnly projects.compatLib.compatLibVR
                compileOnly projects.compatLib.compatLibVS
                compileOnly projects.compatLib.compatLibVT
                compileOnly projects.compatLib.compatLibVU
                compileOnly projects.compatLib.compatLibVV
                compileOnly projects.compatLib.compatLibVBaklava
            }
        }
    }
}

final String ANDROID_TOP = "${rootDir}"
final String FRAMEWORK_PREBUILTS_DIR = "${ANDROID_TOP}/prebuilts/libs/"

android {
    namespace = 'com.android.launcher3'
    defaultConfig {
        versionCode 1
        versionName "1.0"

        buildConfigField "boolean", "IS_STUDIO_BUILD", "false"
        buildConfigField "boolean", "QSB_ON_FIRST_SCREEN", "true"
        buildConfigField "boolean", "IS_DEBUG_DEVICE", "false"
        buildConfigField "boolean", "WIDGET_ON_FIRST_SCREEN", "true"
        buildConfigField "boolean", "WIDGETS_ENABLED", "true"
        buildConfigField "boolean", "NOTIFICATION_DOTS_ENABLED", "true"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def channel = variant.productFlavors.last().name
            outputFileName = "Lawnchair.${variant.versionName}.$channel.${variant.buildType.name}.apk"
        }
    }

    androidResources {
        generateLocaleConfig true
    }

    buildFeatures {
        aidl true
        buildConfig true
        resValues true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    // The flavor dimensions for build variants (e.g. aospWithQuickstep, aospWithoutQuickstep)
    // See: https://developer.android.com/studio/build/build-variants#flavor-dimensions
    flavorDimensions = ["app", "recents"]

    productFlavors {
        aosp {
            dimension "app"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }

        l3go {
            dimension "app"
            applicationId 'com.android.launcher3'
            testApplicationId 'com.android.launcher3.tests'
        }

        withQuickstep {
            dimension "recents"

            minSdk 33
        }

        withoutQuickstep {
            dimension "recents"
        }
    }

    androidComponents {
        beforeVariants(
                selector()
                        .withBuildType("release")
                        .withFlavor("color", "blue")
        ) { variantBuilder ->
            variantBuilder.enable = false
        }
    }

    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src', 'src_plugins', 'tests/shared', 'system_ext', 'tests/multivalentTests/shared', 'systemui_core/shared', 'shared/src']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDirs = ['protos/', 'quickstep/protos_overrides/']
            }
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src', 'tests/tapl']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        androidTestDebug {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        aosp {
            java.srcDirs = ['src_flags', 'src_shortcuts_overrides']
        }

        aospWithoutQuickstep {
            java.srcDirs = ['src_no_quickstep']
            manifest.srcFile "AndroidManifest.xml"
        }

        aospWithQuickstep {
            manifest.srcFile "quickstep/AndroidManifest-launcher.xml"
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src']
            manifest.srcFile "go/AndroidManifest.xml"
        }

        l3goWithoutQuickstepDebug {
            manifest.srcFile "AndroidManifest.xml"
        }

        l3goWithQuickstepDebug {
            manifest.srcFile "quickstep/AndroidManifest-launcher.xml"
        }

        withoutQuickstep {
            java.srcDirs = ['src_ui_overrides']
        }

        withQuickstep {
            res.srcDirs = ['quickstep/res', 'quickstep/recents_ui_overrides/res']
            java.srcDirs = ['quickstep/src', 'quickstep/recents_ui_overrides/src', 'quickstep/src_protolog', 'quickstep/dagger']
            manifest.srcFile "quickstep/AndroidManifest.xml"
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    implementation libs.androidx.core.ktx
    implementation libs.androidx.recyclerview
    implementation libs.androidx.dynamicanimation
    implementation libs.androidx.preference
    implementation libs.androidx.slice.builders
    implementation libs.androidx.graphics.shapes
    implementation libs.androidx.window
    implementation libs.androidx.core.animation
    implementation libs.androidx.material3
    implementation libs.material
    implementation libs.kotlinx.coroutines

    implementation libs.protobuf.javalite

    api libs.lottie
//    implementation libs.dagger
//    ksp libs.dagger.compiler
    ksp libs.dagger.compiler
    implementation libs.dagger.hilt.android
    ksp libs.dagger.hilt.compiler

    debugImplementation libs.leakcanary.android

    testImplementation libs.junit
    androidTestImplementation libs.mockito.core
    androidTestImplementation libs.dexmaker
    androidTestImplementation libs.dexmaker.mockito
    androidTestImplementation libs.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.uiautomator.v18
    androidTestImplementation libs.annotation
}

ksp {
    arg("dagger.hilt.disableModulesHaveInstallInCheck", "true")
}

dependencies {
    implementation projects.iconloaderlib
    implementation projects.searchuilib
    implementation projects.animationlib
    implementation projects.msdllib
    implementation projects.contextualeducationlib
    implementation projects.viewcapturelib
    implementation projects.displaylib
    implementation projects.mechanics

    // Recents lib dependency
    withQuickstepCompileOnly projects.hiddenApi
    withQuickstepImplementation projects.shared
    withQuickstepImplementation projects.animation
    withQuickstepImplementation projects.unfold
    withQuickstepImplementation projects.log
    withQuickstepCompileOnly projects.plugin
    withQuickstepImplementation projects.plugincore
    withQuickstepCompileOnly projects.common
    withQuickstepCompileOnly projects.utils

    // QuickSwitch Compat
    withQuickstepImplementation projects.compatLib
    withQuickstepImplementation projects.compatLib.compatLibVQ
    withQuickstepImplementation projects.compatLib.compatLibVR
    withQuickstepImplementation projects.compatLib.compatLibVS
    withQuickstepImplementation projects.compatLib.compatLibVT
    withQuickstepImplementation projects.compatLib.compatLibVU
    withQuickstepImplementation projects.compatLib.compatLibVV
    withQuickstepImplementation projects.compatLib.compatLibVBaklava
    withQuickstepImplementation projects.wmshell
    withQuickstepImplementation projects.flags
    withQuickstepImplementation projects.dagger
    withQuickstepImplementation projects.concurrent
    withQuickstepImplementation projects.modules.widgetpicker
//    withQuickstepImplementation projects.compose

}

dependencies {
    implementation fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-statsd-16.jar')
    implementation fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'WindowManager-Shell-16.jar')
    withQuickstepCompileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'framework-16.jar')
}

//def protocVersion = '4.33.1'//'3.8.0'
//
//protobuf {
//    // Configure the protoc executable
//    protoc {
//        if (osdetector.os == "osx") {
//            artifact = "com.google.protobuf:protoc:${protocVersion}:osx-x86_64"
//        } else {
//            //        artifact = "com.google.protobuf:protoc:${protocVersion}${PROTO_ARCH_SUFFIX}"
//            artifact = "com.google.protobuf:protoc:${protocVersion}"
//        }
//    }
//    generateProtoTasks {
//        all().each { task ->
//            task.builtins {
//                remove java
//                java {
//                    option "lite"
//                }
//            }
//        }
//    }
//}
